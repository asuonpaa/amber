#!amber
# Copyright 2020 The Amber Authors.
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     https://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

DEVICE_FEATURE shaderStorageImageMultisample

SHADER compute compute_shader GLSL
#version 430

layout(local_size_x = 16, local_size_y = 16) in;
uniform layout(set=0, binding=0, rgba8) image2DMS texture;
uniform layout(set=0, binding=1, rgba8) image2D sample0;
uniform layout(set=0, binding=2, rgba8) image2D sample1;
uniform layout(set=0, binding=3, rgba8) image2D sample2;
uniform layout(set=0, binding=4, rgba8) image2D sample3;
void main()
{
    ivec2 uv = ivec2(gl_GlobalInvocationID.xy);
    imageStore(texture, uv, 0, vec4(1, 0, 0, 1));
    imageStore(texture, uv, 1, vec4(0, 1, 0, 1));
    imageStore(texture, uv, 2, vec4(0, 0, 1, 1));
    imageStore(texture, uv, 3, vec4(1, 1, 0, 1));
    imageStore(sample0, uv, imageLoad(texture, uv, 3));
    imageStore(sample1, uv, imageLoad(texture, uv, 2));
    imageStore(sample2, uv, imageLoad(texture, uv, 1));
    imageStore(sample3, uv, imageLoad(texture, uv, 0));
}
END

# TODO Ari: When using IMAGE for multisample it cannot refer to a buffer
BYTE_BUFFER texture_data SIZE_BYTES 256*256*4 FILL 0
BYTE_BUFFER sample0_data SIZE_BYTES 256*256*4 FILL 0
BYTE_BUFFER sample1_data SIZE_BYTES 256*256*4 FILL 0
BYTE_BUFFER sample2_data SIZE_BYTES 256*256*4 FILL 0
BYTE_BUFFER sample3_data SIZE_BYTES 256*256*4 FILL 0
IMAGE texture FORMAT R8G8B8A8_UNORM DIM_2D WIDTH 256 HEIGHT 256 SAMPLES 4 BUFFERS texture_data
IMAGE sample0 FORMAT R8G8B8A8_UNORM DIM_2D WIDTH 256 HEIGHT 256 BUFFERS sample0_data
IMAGE sample1 FORMAT R8G8B8A8_UNORM DIM_2D WIDTH 256 HEIGHT 256 BUFFERS sample1_data
IMAGE sample2 FORMAT R8G8B8A8_UNORM DIM_2D WIDTH 256 HEIGHT 256 BUFFERS sample2_data
IMAGE sample3 FORMAT R8G8B8A8_UNORM DIM_2D WIDTH 256 HEIGHT 256 BUFFERS sample3_data

PIPELINE compute pipeline
  ATTACH compute_shader
  BIND IMAGE texture AS storage_image DESCRIPTOR_SET 0 BINDING 0
  BIND IMAGE sample0 AS storage_image DESCRIPTOR_SET 0 BINDING 1
  BIND IMAGE sample1 AS storage_image DESCRIPTOR_SET 0 BINDING 2
  BIND IMAGE sample2 AS storage_image DESCRIPTOR_SET 0 BINDING 3
  BIND IMAGE sample3 AS storage_image DESCRIPTOR_SET 0 BINDING 4
END

RUN pipeline 16 16 1

EXPECT sample0_data IDX 0 0 SIZE 256 256 EQ_RGBA 255 255   0 255
EXPECT sample1_data IDX 0 0 SIZE 256 256 EQ_RGBA   0   0 255 255
EXPECT sample2_data IDX 0 0 SIZE 256 256 EQ_RGBA   0 255   0 255
EXPECT sample3_data IDX 0 0 SIZE 256 256 EQ_RGBA 255   0   0 255
